plugins {
    id "java-library"
    id "org.sonarqube" version "5.0.0.4638"
}

allprojects {
  ext.baseVersion = "0.1"
  ext.snapshotVersion = true

  group = "org.sonarqube"
  version = "$baseVersion" + (snapshotVersion ? "-SNAPSHOT" : "")
}

dependencies {
    implementation 'ch.qos.logback:logback-classic:1.2.10'
}


sonar {
    properties {
        property "sonar.projectName", "Example of SonarScanner for Gradle - Multimodule"
        property "sonar.projectKey", "org.sonarqube:gradle-multimodule"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.token", "sqa_6e4c75395d17eb0d0ec0b84c6387ed26208bc305"
        property "sonar.java.binaries", "module-one/src/main/java, module-two/src/main/java"
        property "sonar.scm.disabled", "true"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    sonar {
        properties {
            property "sonar.java.binaries", file("build/classes/java/main").path
            property "sonar.coverage.jacoco.xmlReportPaths", file("build/reports/jacoco/test/jacocoTestReport.xml").path
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}
